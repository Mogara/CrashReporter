#-------------------------------------------------
#
# Project created by QtCreator 2014-03-01T23:19:56
#
#-------------------------------------------------

QT       += network

greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TARGET = QSanSMTPClient
TEMPLATE = app


SOURCES += \
    main.cpp \
    mainwindow.cpp \
    src/emailaddress.cpp \
    src/mimeattachment.cpp \
    src/mimefile.cpp \
    src/mimehtml.cpp \
    src/mimeinlinefile.cpp \
    src/mimemessage.cpp \
    src/mimepart.cpp \
    src/mimetext.cpp \
    src/smtpclient.cpp \
    src/quotedprintable.cpp \
    src/mimemultipart.cpp \
    src/mimecontentformatter.cpp

HEADERS  += \
    mainwindow.h \
    src/emailaddress.h \
    src/mimeattachment.h \
    src/mimefile.h \
    src/mimehtml.h \
    src/mimeinlinefile.h \
    src/mimemessage.h \
    src/mimepart.h \
    src/mimetext.h \
    src/smtpclient.h \
    src/SmtpMime \
    src/quotedprintable.h \
    src/mimemultipart.h \
    src/mimecontentformatter.h

INCLUDEPATH += src

TRANSLATIONS += QSanSMTPClient.ts

win32-msvc*{
    DEFINES += _CRT_SECURE_NO_WARNINGS
    !contains(QMAKE_HOST.arch, x86_64) {
        DEFINES += WIN32
        LIBS += -L"$$_PRO_FILE_PWD_/lib/win/x86"
    } else {
        DEFINES += WIN64
        LIBS += -L"$$_PRO_FILE_PWD_/lib/win/x64"
    }
}
win32-g++{
    DEFINES += WIN32
    LIBS += -L"$$_PRO_FILE_PWD_/lib/win/MinGW"
    DEFINES += GPP
}
winrt{
    DEFINES += _CRT_SECURE_NO_WARNINGS
    DEFINES += WINRT
    LIBS += -L"$$_PRO_FILE_PWD_/lib/winrt/x64"
}
macx{
    DEFINES += MAC
    LIBS += -L"$$_PRO_FILE_PWD_/lib/mac/lib"
}
ios{
    DEFINES += IOS
    CONFIG(iphonesimulator){
        LIBS += -L"$$_PRO_FILE_PWD_/lib/ios/simulator/lib"
    }
    else {
        LIBS += -L"$$_PRO_FILE_PWD_/lib/ios/device/lib"
    }
}
linux{
    DEFINES += LINUX
    !contains(QMAKE_HOST.arch, x86_64) {
        LIBS += -L"$$_PRO_FILE_PWD_/lib/linux/x86"
    }
    else {
        LIBS += -L"$$_PRO_FILE_PWD_/lib/linux/x64"
    }
}

LIBS += -lPasswordGetter
